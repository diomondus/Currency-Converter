version '1.1'

apply plugin: 'java'

sourceCompatibility = 9

repositories {
    mavenCentral()
}

ext.moduleName = 'com.butilov'

compileJava {
    inputs.property("moduleName", moduleName)
    doFirst {
        options.compilerArgs = [
                '--module-path', classpath.asPath,
        ]
        classpath = files()
    }
}

compileTestJava {
    inputs.property("moduleName", moduleName)
    doFirst {
        options.compilerArgs = [
                '--module-path', classpath.asPath,
                '--add-modules', 'junit',
                '--add-reads', "$moduleName=junit",
                '--patch-module', "$moduleName=" + files(sourceSets.test.java.srcDirs).asPath,
        ]
        classpath = files()
    }
}

test {
    inputs.property("moduleName", moduleName)
    doFirst {
        jvmArgs = [
                '--module-path', classpath.asPath,
                '--add-modules', 'ALL-MODULE-PATH',
                '--add-reads', "$moduleName=junit",
                '--patch-module', "$moduleName=" + files(sourceSets.test.java.outputDir).asPath,
        ]
        classpath = files()
    }
}

jar {
    inputs.property("moduleName", moduleName)
    manifest {
        attributes('Automatic-Module-Name': moduleName)
    }
}

dependencies {
    compile group: 'org.springframework', name: 'spring-core', version: '5.0.4.RELEASE'
    compile group: 'org.springframework', name: 'spring-context', version: '5.0.4.RELEASE'

    compile("org.springframework:spring-web:5.0.4.RELEASE")
    compile("com.fasterxml.jackson.core:jackson-databind:2.9.4")

    compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.16.20'

    testCompile group: 'junit', name: 'junit', version: '4.12'
}
